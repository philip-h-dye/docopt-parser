// __version__ = '0.5.7'

docopt = intro* usage other_sections trailing* newline* EOF

other_sections = other*

other = blankline ( operand_section / options_section / description )

// Intro -- typically a single line 
// -----

intro = newline* !usage_entry line+ newline+

// Usage Patterns
// --------------

usage = newline* usage_entry newline* usage_line+

usage_entry = r'(?i)Usage\s*:'

usage_line = usage_pattern newline / usage_pattern EOF
usage_pattern = program choice?

program = string_no_whitespace

expression = ( optional / required / argument )+

optional = LBRACKET choice RBRACKET repeated?
required = LPAREN choice RPAREN repeated?
argument = ( option / operand / command ) repeated?
repeated = '...'

choice = expression ( BAR choice )?

option = long / short
// short option allowing for stacking or an immediate option-argument
//   semantic analysis necessary to determine such
//   and frankly, necessary for short_no_arg also.  If short_no_arg is
//   first, '-fuz' becomes [ short_no_arg '-f', command 'uz' ]
short = short_stacked / short_no_arg
short_no_arg = r'-[\w]'
short_stacked = r'-[\w]+'
long = long_with_eq_arg / long_no_arg
long_no_arg = r'--[\w][-_\w]*'
long_with_eq_arg = long_with_eq_caps / long_with_eq_angle
long_with_eq_caps = r"--[\w][-_\w]*=[A-Z][_A-Z]*"
long_with_eq_angle = r"--[\w][-_\w]*=<[-_:\w]+>"

operand = operand_all_caps / operand_angled
operand_all_caps = r'\b[A-Z][_A-Z]+\b'
operand_angled = r'<[-_:\w]+>'

command = string_no_whitespace

// Program Description
// -------------------

description = line+
line = !operand_or_option ( word+ newline / word+ EOF )
word = string_no_whitespace

// Operand / Positional Arguments with help text
// ---------------------------------------------

operand_section = operand_intro? operand_line+
operand_intro = line
operand_line = operand_detail newline / operand_detail EOF
operand_detail = operand operand_help?
operand_help = word+ ( newline !operand_or_option operand_help )*

// Options details with help text
// ------------------------------

options_section = newline* options_intro? option_line+ newline*

options_intro = line

option_line = option_detail newline / option_detail EOF
option_detail = option_list option_help?

option_list = option_single (COMMA? option_list )?
option_single = short_with_arg / long_with_arg / short_no_arg / long_no_arg
option_help = word+ ( newline !operand_or_option option_help )*

short_with_arg = short_no_arg operand
long_with_arg = long_with_eq_arg / long_with_gap_arg
long_with_gap_arg = long_no_arg operand

// Trailing Text -- catchall
// -------------

trailing = trailing_line+
trailing_line = word+ newline / word+ EOF

// General Utility
// ---------------

operand_or_option = r'([-<]|[A-Z][A-Z])'

blankline = &newline newline+

newline = r'[\n]'

LBRACKET = '['
RBRACKET = ']'
LPAREN = '('
RPAREN = ')'
BAR = '|'
COMMA = ','

string_no_whitespace = r'[^\s\n\[\]\(\)\|]+'
// Without \[\]\(\)\|, optional/required/choice become sequences of commands
